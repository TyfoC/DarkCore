[BITS 32]
[EXTERN TicksCount_]
[EXTERN CurrentThread_]
[EXTERN TaskSwitchingEnabled_]
[GLOBAL Schedule]
[GLOBAL SwitchThreadNext]

%DEFINE THREAD_STATE_RUNNING								0
%DEFINE THREAD_STATE_READY_TO_RUN							1
%DEFINE THREAD_STATE_SLEEPING								2
%DEFINE THREAD_STATE_SUSPENDED								3

%DEFINE THREAD_OFFSET_STATE									0
%DEFINE THREAD_OFFSET_SKIP_TICKS_COUNT						4
%DEFINE THREAD_OFFSET_WORKING_TICKS_COUNT					8
%DEFINE THREAD_OFFSET_PAGING_DIRECTORY						12
%DEFINE THREAD_OFFSET_REGISTER_GS							16
%DEFINE THREAD_OFFSET_REGISTER_FS							20
%DEFINE THREAD_OFFSET_REGISTER_ES							24
%DEFINE THREAD_OFFSET_REGISTER_DS							28
%DEFINE THREAD_OFFSET_REGISTER_DI							32
%DEFINE THREAD_OFFSET_REGISTER_SI							36
%DEFINE THREAD_OFFSET_REGISTER_BP							40
%DEFINE THREAD_OFFSET_REGISTER_SP							44
%DEFINE THREAD_OFFSET_REGISTER_BASE							48
%DEFINE THREAD_OFFSET_REGISTER_DATA							52
%DEFINE THREAD_OFFSET_REGISTER_COUNTER						56
%DEFINE THREAD_OFFSET_REGISTER_ACCUMULATOR					60
%DEFINE THREAD_OFFSET_REGISTER_IP							64
%DEFINE THREAD_OFFSET_REGISTER_CS							68
%DEFINE THREAD_OFFSET_REGISTER_FLAGS						72
%DEFINE THREAD_OFFSET_NEXT									76
%DEFINE THREAD_OFFSET_CURRENT_PATH_STRING_VIRTUAL_ADDRESS	80

ScheduleBuffer:
	.AccumulatorRegister:	DD 0
Schedule:
	INC DWORD [TicksCount_]

	MOV [ScheduleBuffer.AccumulatorRegister], EAX

	CMP DWORD [TaskSwitchingEnabled_], 0
	JE ScheduleDone

	MOV EAX, [CurrentThread_]
	CMP DWORD [EAX + THREAD_OFFSET_WORKING_TICKS_COUNT], 0
	JNE ScheduleNotSwitched

	MOV [EAX + THREAD_OFFSET_REGISTER_GS], GS
	MOV [EAX + THREAD_OFFSET_REGISTER_FS], FS
	MOV [EAX + THREAD_OFFSET_REGISTER_ES], ES
	MOV [EAX + THREAD_OFFSET_REGISTER_DS], DS
	MOV [EAX + THREAD_OFFSET_REGISTER_DI], EDI
	MOV [EAX + THREAD_OFFSET_REGISTER_SI], ESI
	MOV [EAX + THREAD_OFFSET_REGISTER_BP], EBP
	MOV [EAX + THREAD_OFFSET_REGISTER_SP], ESP
	ADD DWORD [EAX + THREAD_OFFSET_REGISTER_SP], 12
	MOV [EAX + THREAD_OFFSET_REGISTER_BASE], EBX
	MOV [EAX + THREAD_OFFSET_REGISTER_DATA], EDX
	MOV [EAX + THREAD_OFFSET_REGISTER_COUNTER], ECX

	POP ECX																;	EIP
	MOV [EAX + THREAD_OFFSET_REGISTER_IP], ECX
	POP ECX																;	CS
	MOV [EAX + THREAD_OFFSET_REGISTER_CS], ECX
	POP ECX																;	EFLAGS
	MOV [EAX + THREAD_OFFSET_REGISTER_FLAGS], ECX

	MOV ECX, [ScheduleBuffer.AccumulatorRegister]						;	EAX
	MOV [EAX + THREAD_OFFSET_REGISTER_ACCUMULATOR], ECX

	MOV DWORD [EAX + THREAD_OFFSET_STATE], THREAD_STATE_READY_TO_RUN
	MOV DWORD [EAX + THREAD_OFFSET_WORKING_TICKS_COUNT], 100
	
	MOV EAX, [EAX + THREAD_OFFSET_NEXT]
	MOV [CurrentThread_], EAX

	MOV ECX, [EAX + THREAD_OFFSET_PAGING_DIRECTORY]
	MOV CR3, ECX

	MOV ECX, [EAX + THREAD_OFFSET_REGISTER_ACCUMULATOR]
	MOV [ScheduleBuffer.AccumulatorRegister], ECX

	MOV ECX, [EAX + THREAD_OFFSET_REGISTER_COUNTER]
	MOV EDX, [EAX + THREAD_OFFSET_REGISTER_DATA]
	MOV EBX, [EAX + THREAD_OFFSET_REGISTER_BASE]
	MOV EBP, [EAX + THREAD_OFFSET_REGISTER_BP]
	MOV ESP, [EAX + THREAD_OFFSET_REGISTER_SP]
	MOV ESI, [EAX + THREAD_OFFSET_REGISTER_SI]
	MOV EDI, [EAX + THREAD_OFFSET_REGISTER_DI]
	MOV DS, [EAX + THREAD_OFFSET_REGISTER_DS]
	MOV ES, [EAX + THREAD_OFFSET_REGISTER_ES]
	MOV FS, [EAX + THREAD_OFFSET_REGISTER_FS]
	MOV GS, [EAX + THREAD_OFFSET_REGISTER_GS]

	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_FLAGS]
	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_CS]
	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_IP]

	JMP ScheduleDone
ScheduleNotSwitched:
	DEC DWORD [EAX + THREAD_OFFSET_WORKING_TICKS_COUNT]
ScheduleDone:
	MOV AL, 0x20
	OUT 0x20, AL
	MOV EAX, [ScheduleBuffer.AccumulatorRegister]
	IRET

SwitchThreadNext:
	POP EAX
	MOV EAX, [CurrentThread_]
	MOV EAX, [EAX + THREAD_OFFSET_NEXT]
	MOV [CurrentThread_], EAX

	MOV ECX, [EAX + THREAD_OFFSET_PAGING_DIRECTORY]
	MOV CR3, ECX

	MOV ECX, [EAX + THREAD_OFFSET_REGISTER_COUNTER]
	MOV EDX, [EAX + THREAD_OFFSET_REGISTER_DATA]
	MOV EBX, [EAX + THREAD_OFFSET_REGISTER_BASE]
	MOV ESP, [EAX + THREAD_OFFSET_REGISTER_SP]
	MOV EBP, [EAX + THREAD_OFFSET_REGISTER_BP]
	MOV ESI, [EAX + THREAD_OFFSET_REGISTER_SI]
	MOV EDI, [EAX + THREAD_OFFSET_REGISTER_DI]
	MOV DS, [EAX + THREAD_OFFSET_REGISTER_DS]
	MOV ES, [EAX + THREAD_OFFSET_REGISTER_ES]
	MOV FS, [EAX + THREAD_OFFSET_REGISTER_FS]
	MOV GS, [EAX + THREAD_OFFSET_REGISTER_GS]

	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_FLAGS]
	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_CS]
	PUSH DWORD [EAX + THREAD_OFFSET_REGISTER_IP]

	MOV EAX, [EAX + THREAD_OFFSET_REGISTER_ACCUMULATOR]
	IRET